# set main memory address table
setenv addr_script 0x02000000
setenv addr_eeprom 0x02003000
setenv addr_linux  0x02004000
setenv addr_dtb    0x04000000

# set default Linux kernel boot parameters
setenv bootargs console=ttyPS0,115200 root=/dev/mmcblk0p2 ro rootfstype=ext4 earlyprintk rootwait uio_pdrv_genirq.of_id="generic-uio"

if test ${hw_rev} = '2.0'
then  
  setenv high     0x10000000
  setenv bootargs ${bootargs} mem=256M
else 
  setenv high     0x20000000
fi
echo Set devicetree and ramdisk high loading address to ${high}

setenv fdt_high    ${high}
setenv initrd_high ${high}

# set additional Linux kernel boot parameters, mostly debug options
#setenv bootargs ${bootargs} loglevel=7 dyndbg="file axidmatest.c +p"
# additional systemd logging
#setenv bootargs ${bootargs} systemd.log_level=debug systemd.log_target=kmsg log_buf_len=1M

echo Loading from SD card (FAT file system) to memory
mmcinfo
fatload mmc 0 ${addr_linux} uImage
fatload mmc 0 ${addr_dtb}   devicetree.dtb

echo Booting Linux kernel with ramdisk and devicetree
setenv sdboot bootm ${addr_linux} - ${addr_dtb}
setenv bootdelay 0
bootd
