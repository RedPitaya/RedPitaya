# load extra enviroment form eeprom
i2c dev 0
# Added an extra offset of 4 bytes from 0x1800 -> 0x1804, since the memory was not initially initialized correctly and there are always 4 bytes of garbage at the beginning of the data.
eeprom read  0 0x50 0 0x1804 0x400
env import -b 0 0x400 hw_rev serial ethaddr

# set main memory address table
setenv addr_script 0x02000000
setenv addr_eeprom 0x02003000
setenv addr_linux  0x02004000
setenv addr_dtb    0x04000000
setenv addr_fpga   0x10000000
setenv zynq        z7020

# set default Linux kernel boot parameters
setenv bootargs console=ttyPS0,115200 root=/dev/mmcblk0p2 ro rootfstype=ext4 earlyprintk rootwait uio_pdrv_genirq.of_id="generic-uio"

# Worked only with 250 12 (1Gb Ram)

if test ${hw_rev} == 'STEM_250-12_v1.0'
then
  setenv dts_path z20_250_1_0
fi

if test ${hw_rev} == 'STEM_250-12_v1.1'
then
  setenv dts_path z20_250
fi

if test ${hw_rev} == 'STEM_250-12_v1.2'
then
  setenv dts_path z20_250
fi

if test ${hw_rev} == 'STEM_250-12_v1.2a'
then
  setenv bootargs ${bootargs} mem=512M
  setenv high     0x20000000
  setenv dts_path z20_250a
fi

if test ${hw_rev} == 'STEM_250-12_120'
then
  setenv dts_path z20_250
fi

fatload mmc 0 ${addr_fpga} fpga/${dts_path}/barebones/fpga.bit
fpga loadb ${zynq} $addr_fpga $filesize

setenv high     0x40000000

echo Set devicetree and ramdisk high loading address to ${high}

setenv fdt_high    ${high}
setenv initrd_high ${high}

# set additional Linux kernel boot parameters, mostly debug options
# setenv bootargs ${bootargs} loglevel=7 dyndbg="file axidmatest.c +p"
# additional systemd logging
# setenv bootargs ${bootargs} systemd.log_level=debug systemd.log_target=console  log_buf_len=1M loglevel=3

echo Loading from SD card (FAT file system) to memory
mmcinfo
echo Load uImage
fatload mmc 0 ${addr_linux} uImage
echo Load dts/${dts_path}/devicetree.dtb
fatload mmc 0 ${addr_dtb}   dts/${dts_path}/devicetree.dtb

echo Booting Linux kernel with ramdisk and devicetree
setenv sdboot bootm ${addr_linux} - ${addr_dtb}
setenv bootdelay 0
bootd
